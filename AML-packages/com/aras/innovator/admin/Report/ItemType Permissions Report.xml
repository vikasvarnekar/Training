<AML>
 <Item type="Report" id="2D3FFE2E01494679AAB6095A2702C774" action="add">
  <description />
  <location>server</location>
  <report_query><![CDATA[<Item type="ItemType" where="[ITEMTYPE].id='{@id}'" action="get" select="name,default_lifecycle(name)">
 <default_lifecycle>
  <Item type="Life Cycle Map" action="get" select="name">
   <Relationships>
    <Item type="Life Cycle State" action="get" select="x,name,state_permission_id(name)">
     <state_permission_id>
      <Item type="Permission" action="get" select="name">
       <Relationships>
        <Item type="Access" action="get" select="sort_order,related_id(name),can_get,can_update,can_delete,can_change_access"/>
       </Relationships>
      </Item>
     </state_permission_id>
    </Item>
   </Relationships>
  </Item>
 </default_lifecycle>
 <Relationships>
  <Item type="Allowed Permission" action="get" select="is_default,related_id">
   <related_id>
    <Item type="Permission" action="get" select="name">
     <Relationships>
      <Item type="Access" action="get" select="related_id(name),can_get,can_update,can_delete,can_change_access"/>
     </Relationships>
    </Item>
   </related_id>
  </Item>
  <Item type="Can Add" action="get" select="can_add,related_id(name)"/>
 </Relationships>
</Item>]]></report_query>
  <target>one window</target>
  <type>item</type>
  <xsl_stylesheet><![CDATA[<xsl:stylesheet version="1.0" 
     xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
     xmlns:msxsl="urn:schemas-microsoft-com:xslt" 
     xmlns:user="urn:user-scripts" 
     xmlns:aras="http://www.aras.com"
     >
 <xsl:output method="html" omit-xml-declaration="yes" standalone="yes" indent="yes" cdata-section-elements="script msxsl:script"/>
  
  <xsl:template match="/">
   <xsl:apply-templates/>
  </xsl:template>

  <xsl:template match="/*[local-name()='Envelope']">
   <xsl:apply-templates/>
  </xsl:template>

  <xsl:template match="/*[local-name()='Envelope']/*[local-name()='Body']">
   <xsl:apply-templates/>
  </xsl:template>

  <xsl:template match="/*[local-name()='Envelope']/*[local-name()='Body']/Result">
   <xsl:apply-templates select="Item"/>
  </xsl:template>
  
  <xsl:template match="Item[@type='ItemType']">
   <html>
    <head>
     <title>ItemType Permissions Report for &quot;<xsl:value-of select="name"/>&quot;</title>
    </head>
    <style>
     <![CDATA[
           span.title{font-family:Verdana; font-size:16pt; font-color: red;}
           td {font-family:Arial; font-size:8pt; padding-left:5px; padding-right:5px; border-width:1;}
           td.headLabel {font-weight:bold; text-align:right; vertical-align:bottom;}
           td.headValue {font-size:11pt; padding-5px:10px; padding-right:15px;}
           td.label {font-weight:bold; text-align:center;}
           td.value {border-style:solid;}
           td.section {font-size:11pt; background-color:#dddddd;}
           table {width: 750px}
           table.border {empty-cells:show; border-collapse:collapse; border-style:solid; border-width:1}
           table.noborder {empty-cells:show; border-collapse:collapse; border-width:0}
           th {font-family:Arial; font-size:8pt; border-style:solid; border-width:1; padding-right:5px; padding-left:5px; text-align:center;}

     ]]]]><![CDATA[> 
    </style>
   <body>
    <span class="title">ItemType Permissions Report for &quot;<xsl:value-of select="name"/>&quot;</span>
    <br/>
    <br/>
    <table class="border">
     <tr><td class="section" colspan="7"><b>Allowed Permissions</b></td></tr>
     <tr>
      <th>Default</th>
      <th>Permission Name</th>
      <th>Identity Name</th>
      <th>Can Get</th>
      <th>Can Update</th>
      <th>Can Delete</th>
      <th>Can Change Access</th>
     </tr>
     <xsl:apply-templates select="Relationships/Item[@type='Allowed Permission']/related_id/Item[@type='Permission']">
      <xsl:sort select="../../sort_order"/>
     </xsl:apply-templates>
     <tr><td class="section" colspan="7"><b>State Permissions</b></td></tr>
     <tr>
      <th>State Name</th>
      <th>Permission Name</th>
      <th>Identity Name</th>
      <th>Can Get</th>
      <th>Can Update</th>
      <th>Can Delete</th>
      <th>Can Change Access</th>
     </tr>
     <xsl:apply-templates select="default_lifecycle/Item/Relationships/Item[@type='Life Cycle State']">
      <xsl:sort select="x" data-type="number"/>
     </xsl:apply-templates>
     <tr><td class="section" colspan="7"><b>Can Add</b></td></tr>
     <tr>
      <th>Identity Name</th>
      <th>Can Add</th>
     </tr>
     <xsl:apply-templates select="Relationships/Item[@type='Can Add']"/>
    </table>
   </body>
  </html>
 </xsl:template>
 
 <xsl:template match="Item[@type='Life Cycle State'][not(state_permission_id)]">
  <tr>
   <td class="value"><xsl:value-of select="name"/></td>
   <td class="value" colspan="6" align="center">No State Permission defined</td>
  </tr>
 </xsl:template>

 <xsl:template match="Item[@type='Life Cycle State'][state_permission_id!='']">
  <xsl:apply-templates select="state_permission_id/Item"/>
 </xsl:template>
 
 <xsl:template match="Item[@type='Permission']">
   <tr>
    <td class="value">
     <xsl:attribute name="rowspan"><xsl:value-of select="count(Relationships/Item[@type='Access'])"/></xsl:attribute>
     <xsl:choose>
      <xsl:when test="../../@type='Allowed Permission'">
       <xsl:call-template name="YesNo">
        <xsl:with-param name="val"><xsl:value-of select="../../is_default"/></xsl:with-param>
       </xsl:call-template>
      </xsl:when>
      <xsl:otherwise><xsl:value-of select="../../name"/></xsl:otherwise>
     </xsl:choose>
    </td>
    <td class="value">
     <xsl:attribute name="rowspan"><xsl:value-of select="count(Relationships/Item[@type='Access'])"/></xsl:attribute>
     <xsl:value-of select="name"/>
    </td>
    <xsl:call-template name="Access">
     <xsl:with-param name="node" select="Relationships/Item[@type='Access'][1]"/>
    </xsl:call-template>
   </tr>
   <xsl:for-each select="Relationships/Item[@type='Access'][position()!='1']">
    <tr>
     <xsl:call-template name="Access">
      <xsl:with-param name="node" select="."/>
     </xsl:call-template>
    </tr>
   </xsl:for-each>
 </xsl:template>
 
 <xsl:template name="Access">
  <xsl:param name="node"/>
  
  <td class="value">
   <xsl:value-of select="$node/related_id/Item/name"/>
  </td>
  <td class="value" align="center">
   <xsl:call-template name="YesNo">
    <xsl:with-param name="val"><xsl:value-of select="$node/can_get"/></xsl:with-param>
   </xsl:call-template>
  </td>
  <td class="value" align="center">
   <xsl:call-template name="YesNo">
    <xsl:with-param name="val"><xsl:value-of select="$node/can_update"/></xsl:with-param>
   </xsl:call-template>
  </td>
  <td class="value" align="center">
   <xsl:call-template name="YesNo">
    <xsl:with-param name="val"><xsl:value-of select="$node/can_delete"/></xsl:with-param>
   </xsl:call-template>
  </td>
  <td class="value" align="center">
   <xsl:call-template name="YesNo">
    <xsl:with-param name="val"><xsl:value-of select="$node/can_change_access"/></xsl:with-param>
   </xsl:call-template>
  </td>
 </xsl:template>
 
 <xsl:template match="Item[@type='Can Add']">
  <tr>
   <td class="value">
    <xsl:value-of select="related_id/Item/name"/>    
   </td>
   <td class="value" align="center">
    <xsl:call-template name="YesNo">
     <xsl:with-param name="val"><xsl:value-of select="can_add"/></xsl:with-param>
    </xsl:call-template>
   </td>
  </tr>
 </xsl:template>
 
 <xsl:template name="YesNo">
  <xsl:param name="val"/>
  
  <xsl:choose>
   <xsl:when test="$val = '1'">Yes</xsl:when>
   <xsl:otherwise>No</xsl:otherwise>
  </xsl:choose>
 </xsl:template>
 
</xsl:stylesheet>]]></xsl_stylesheet>
  <name>ItemType Permissions Report</name>
 </Item>
</AML>